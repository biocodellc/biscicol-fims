buildscript {
    repositories {
        maven {
            url "http://repo.biocodellc.com/repository/maven-private/"
            credentials {
                username mavenUser
                password mavenPass
            }
        }
        mavenLocal()
    }
    dependencies {
        classpath "org.biocode:gradle-fims:1.1.6"
    }
}

apply plugin: "application"
apply plugin: "org.biocode.fims-web"

import org.biocode.gradle.web.tasks.RestartRemoteJettyTask
import org.biocode.gradle.web.tasks.HotDeployRemoteJettyTask

ext.fimsCommonsVersion = "0.2.3"

fims {
    maven {
        username mavenUser
        password mavenPass
    }
}

repositories {
    add(fims.mavenFims())
    mavenLocal()
}

// need to evaluate childProjects first so we can add the childProject server dependencies to the server configuration
evaluationDependsOnChildren()

dependencies {
    server 'org.glassfish.jersey.media:jersey-media-multipart:2.25'

    server('org.glassfish.jersey.ext:jersey-spring3:2.25') {
        exclude module: 'bean-validator'
        exclude module: 'asm-all-repackaged'
    }
    server 'javax.servlet:javax.servlet-api:3.1.0'
    server 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.6.0'
    server 'com.googlecode.json-simple:json-simple:1.1.1'
    server 'org.springframework.data:spring-data-jpa:1.10.1.RELEASE'
    server group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'
    server 'org.slf4j:slf4j-log4j12:1.7.19'
    server('org.apache.commons:commons-digester3:3.2') {
        exclude module: 'asm'
    }
    server 'commons-cli:commons-cli:1.2'

    server group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile composite.resolveDependency("org.biocode:biocode-fims-commons:${fimsCommonsVersion}", ":biocode-fims-commons")

    doclet fileTree(dir: 'swagger', include: ['swagger-doclet-*'])

    additionalSources(group: 'org.springframework.data', name: 'spring-data-commons', version: '1.10.1.RELEASE', classifier: 'sources') {
        transitive = false
    }
    additionalSources(group: 'org.biocode', name: 'biocode-fims-commons', version: "${fimsCommonsVersion}") {
        transitive = false
    }

    testCompile "junit:junit-dep:4.10"
    integrationTestRuntime group: 'org.aspectj', name: 'aspectjweaver', version: '1.8.9'
    // need to use the following until https://github.com/gradle/gradle/issues/1553 is resolved. Then we can update DependencyResolver dependency substitution code
    integrationTestCompile project(path: ":biocode-fims-commons", configuration: "integrationTestOutput")
//    integrationTestCompile composite.resolveDependency("org.biocode:biocode-fims-commons:${fimsCommonsVersion}", "biocode-fims-commons", "integrationTestOutput")

    subprojects.each {
        project.configurations.server.dependencies.addAll(it.configurations.server.dependencies)
        project.configurations.serverRuntime.dependencies.addAll(it.configurations.serverRuntime.dependencies)
    }
}

//=============================
//= ssh configuration
//=============================

// jetty ssh plugin
remotes {
    biscicol {
        host = 'biscicol.org'
        user = biscicolUser
        password = biscicolPassword
    }
}

//=============================
//= application configuration
//=============================
mainClassName = "biocode.fims.run.FimsPostgresMigrator"

distributions {
    main {
        contents {
            into('lib') {
                from configurations.server
            }
        }
    }
}

startScripts {
    classpath = files(jar.archivePath, configurations.server, configurations.runtime)
}

task ezidUpdate(type: JavaExec) {
    classpath = configurations.server + configurations.compile + sourceSets.main.runtimeClasspath

    main = "biocode.fims.run.EzidUpdator"
}

//=============================
//= deployment configuration
//=============================

updateDependencies {
    remote = remotes.biscicol
    location = remoteLibsDirectory
}

updateDependenciesDev {
    remote = remotes.biscicol
    location = remoteDevLibsDirectory
}

deployFims {
    remote = remotes.biscicol
    remoteLibsDir = remoteLibsDirectory
    remoteWarDir = remoteWarDirectory
}

deployFimsDev {
    remote = remotes.biscicol
    remoteLibsDir = remoteDevLibsDirectory
    remoteWarDir = remoteWarDevDirectory
}

deployFimsLocal {
    deployDir = localWarDirectory
}

task hotDeployFimsDev(type: HotDeployRemoteJettyTask, dependsOn: "deployFimsDev") {
    group = 'Fims'
    remoteWarDir = remoteWarDevDirectory
    remote = remotes.biscicol
}

task hotDeployFims(type: HotDeployRemoteJettyTask, dependsOn: "deployFims") {
    group = 'Fims'
    remoteWarDir = remoteWarDirectory
    remote = remotes.biscicol
}

task restartFims(type: RestartRemoteJettyTask) {
    group = 'Fims'
    jettyPath = biscicolJettyPath
    remote = remotes.biscicol
}

task restartFimsDev(type: RestartRemoteJettyTask) {
    group = 'Fims'
    jettyPath = biscicolJettyDevPath
    remote = remotes.biscicol
}

//=============================
//= Rest api docs configuration
//=============================

generateRestApiDocs {
    swagger {
        apiInfo = "${projectDir}/swagger/api-info.json"
        apiVersions = ["v1.1", "v2"]
        apiBasePath = "/biocode-fims/rest/"
    }
}

//===========================
//= js configuration
//===========================

// specify files that require a specific loading order
ext.externLibsOrdered = ["${webAppDir}/js/externalLibs/lodash.js", "${webAppDir}/js/externalLibs/papaparse.min.js",
                         "${webAppDir}/js/externalLibs/xlsx.min.js", "${webAppDir}/js/externalLibs/xlsx-reader.js"]
javascript.source {
    externLibs {
        js {
            srcDir "src/main/web/js/externalLibs"
            include "*.js"
            // exclude any file that is in the externLibsOrdered list
            exclude {
                it.file in files(externLibsOrdered).files
            }
        }
    }
    app {
        js {
            srcDir "src/main/web/app"
            include "**/*.js"
            exclude "app.js"
            exclude "app.module.js"
            exclude "**/module.js"
        }
    }
    js {
        js {
            srcDir "src/main/web/js"
            include "*.js"
        }
    }
    modules {
        js {
            srcDir "src/main/web/app"
            include "**/module.js"
        }
    }
}

combineJs {
    encoding = "UTF-8"
    source = files("${webAppDirName}/app/app.module.js", javascript.source.modules.js.files, javascript.source.app.js.files, javascript.source.js.js.files)
    dest = file("${webAppDirName}/js/all.js")
}

jsExternalLibs {
    source = externLibsOrdered + javascript.source.externLibs.js.files
    closure {
        compilerOptions.language = "ECMASCRIPT5"
        warningLevel = "QUIET"
    }
}

jsApp {
    source = files("src/main/web/app/app.module.js", javascript.source.modules.js.files, javascript.source.app.js.files, javascript.source.js.js.files)
    closure {
        compilerOptions.language = "ECMASCRIPT5"
    }
}

//=================================
//= angular constants configuration
//=================================

populateJsProps {
    propsFile file("src/main/resources/biscicol-fims.props")
    templateFile file("${webAppDir}/app/app.constants.js.template")
    constantsFile file("${webAppDir}/app/app.constants.js")
    propsMap = ['mapboxToken': 'mapboxAccessToken',
                'naan'  : 'naan']
}
